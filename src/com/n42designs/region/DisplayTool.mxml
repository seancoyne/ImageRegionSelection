<?xml version="1.0" encoding="utf-8"?>
<s:Group  xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" 
		  width="400" 
		  height="300">
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;

			[Bindable]
			public var imgSource:String = "";

			[Bindable]
			public var regionColor:String = "0x000000";
			
			[Bindable]
			public var regionAlpha:Number = 0.33;
			
			[Bindable]
			public var regionCaptionColor:String = "0xFFFFFF";
			
			private var regionDisplays:ArrayCollection = new ArrayCollection();
			
			public function removeAll():void {
				for each (var rd:RegionDisplay in regionDisplays) {
					this.removeElement(rd);
				}
				regionDisplays = new ArrayCollection();
			}
			
			private function getRegionDisplayForRegion(region:Region):RegionDisplay {
				// find the region display item for this region
				for (var i:int = 0; i < regionDisplays.length; i++) {
					var rd:RegionDisplay = regionDisplays.getItemAt(i) as RegionDisplay; 
					if (rd.region == region) {
						// found it, return it
						return rd;
					}
				}
				// didn't find it, return null
				return null;
			}
			
			public function removeRegion(region:Region):void {
				var rd:RegionDisplay = getRegionDisplayForRegion(region);
				if (rd != null) {
					removeRegionDisplay(rd);
				}
			}
			
			private function removeRegionDisplay(regionDisplay:RegionDisplay):void {
				
				// remove from tracking array
				regionDisplays.removeItemAt(regionDisplays.getItemIndex(regionDisplay));
				
				// remove from display
				this.removeElement(regionDisplay);
				
			}
			
			public function addRegion(region:Region):void {
				
				// add a display widget for it
				var regionDisplay:RegionDisplay = new RegionDisplay();
				regionDisplay.bgColor = regionColor;
				regionDisplay.labelColor = regionCaptionColor;
				regionDisplay.region = region;
				this.addElement(regionDisplay);
				
				// track it in the array
				regionDisplays.addItem(regionDisplay);
				
			}
			
		]]>
	</fx:Script>
	
	<s:Image id="imgTarget" width="400" height="300" scaleMode="letterbox" source="{imgSource}"/>	
	
</s:Group>